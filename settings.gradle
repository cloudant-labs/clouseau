pluginManagement {
  includeBuild('build-logic')
}

def scalaLongVsn = "2.13.8"
def scalaShortVsn = scalaLongVsn.tokenize('.').dropRight(1).join('.')

dependencyResolutionManagement {
  repositories {
    mavenCentral { name = 'mavenCentral' }
    flatDir {
      dirs 'deps'
    }
  }

  ext.provenance = [
    'jinterface': 'https://github.com/erlang/otp/tree/master/lib/jinterface',
    '__'        : 'mavenCentral'
  ]

  versionCatalogs {
    libs {
      version('zio-vsn', '2.0.0')
      version('zio-nio', '2.0.0')
      version('zio-config', '3.0.2')
      version('zio-metrics-vsn', '2.0.0')
      version('jinterface', "${System.env.JINTERFACE_VSN}")
      version('sbt', '1.3.5')

      // Scala compiler
      library('scala-compiler', 'org.scala-lang', 'scala-compiler').version(scalaLongVsn)
      library('scala-compiler-bridge', 'org.scala-sbt', "compiler-bridge_$scalaShortVsn").versionRef('sbt')
      library('scala-compiler-interface', 'org.scala-sbt', 'compiler-interface').versionRef('sbt')
      library('scala-library', 'org.scala-lang', 'scala-library').version(scalaLongVsn)
      library('zinc', 'org.scala-sbt', "zinc_$scalaShortVsn").version(scalaLongVsn)

      // This dependency is used by the application.
      library('guava', 'com.google.guava:guava:30.1.1-jre')
      library('zio-lib', 'dev.zio', "zio_$scalaShortVsn").versionRef('zio-vsn')
      library('zio-nio', 'dev.zio', "zio-nio_$scalaShortVsn").versionRef('zio-nio')
      library('jinterface', 'com.ericsson.otp.erlang', 'jinterface').versionRef('jinterface')

      // logger
      library('zio-logging', "dev.zio:zio-logging_$scalaShortVsn:2.0.1")
      library('slf4j-nop', 'org.slf4j:slf4j-nop:2.0.2')

      // config
      library('zio-config', 'dev.zio', "zio-config_$scalaShortVsn").versionRef('zio-config')
      library('zio-config-typesafe', 'dev.zio', "zio-config-typesafe_$scalaShortVsn").versionRef('zio-config')
      library('zio-config-magnolia', 'dev.zio', "zio-config-magnolia_$scalaShortVsn").versionRef('zio-config')

      // metrics
      library('zio-metrics-dropwizard', 'dev.zio', "zio-metrics-dropwizard_$scalaShortVsn").versionRef('zio-metrics-vsn')
      library('zio-zmx', 'dev.zio:zio-zmx_3:0.0.11')

      // testing
      library('junit', 'junit:junit:4.13.2')
      library('zio-tests', 'dev.zio', "zio-test_$scalaShortVsn").versionRef('zio-vsn')
      library('zio-test-junit', 'dev.zio', "zio-test-junit_$scalaShortVsn").versionRef('zio-vsn')
    }
  }
}

rootProject.name = 'ziose'

include 'domain', 'experiments', 'benchmarks', 'actors'
