plugins {
  id 'com.cloudant.scala-library'
  id 'application'
}

apply plugin: 'org.scoverage'
apply plugin: 'org.owasp.dependencycheck'

def group = 'com.cloudant.ziose.experiments'

/*
  gradle run -PmainClass=Hello
*/
application {
  // Define the main class for the application.
  mainClass = group + "." + project.findProperty("mainClass").toString()
}

dependencies {
  implementation(libs.zio.config.magnolia)
  implementation(libs.zio.logging)
  implementation(libs.zio.metrics.dropwizard)
  implementation(project(':actors'))

  runtimeOnly(libs.slf4j.nop)

  testImplementation(libs.zio.test.junit)
}

dependencyCheck {
  failBuildOnCVSS = 5
  failOnError = true
  format = 'XML'
  suppressionFile = "dependency-check-suppressions.xml"
}

jar {
  archivesBaseName = "ziose-${project.hasProperty('type') ? 'test' : 'prod'}-${project.ext.version}"
  destinationDirectory = project.ext.outputDir
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE

  dependsOn(':actors:jar')
  from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  if (project.hasProperty('type')) {
    from sourceSets.toSet().output
  }

  manifest {
    attributes 'Main-Class': 'com.cloudant.ziose.experiments.Hello'
  }
}
