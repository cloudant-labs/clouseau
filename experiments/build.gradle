plugins {
  id 'com.cloudant.scala-library'
  id 'application'
}

apply plugin: 'org.scoverage'
apply plugin: 'org.owasp.dependencycheck'

def group = 'com.cloudant.ziose.experiments'

/*
  gradle run -PmainClass=Hello
*/
application {
  // Define the main class for the application.
  mainClass = group + "." + project.findProperty("mainClass").toString()
}

dependencies {
  implementation(libs.scala.compiler)
  implementation(libs.scala.library)
  implementation(libs.zio.lib)
  implementation(libs.zio.logging)
  implementation(libs.zio.nio)
  implementation(libs.zio.config)
  implementation(libs.zio.config.typesafe)
  implementation(libs.zio.config.magnolia)
  implementation(project(':actors'))
  implementation(libs.jinterface)
  implementation(libs.zio.metrics.dropwizard)
  testImplementation(libs.zio.tests)
  testImplementation(libs.junit)
  testImplementation(libs.zio.test.junit)
}

dependencyCheck {
  failBuildOnCVSS = 5
  failOnError = true
  format = 'XML'
  suppressionFile = "dependency-check-suppressions.xml"
}

jar {
  archivesBaseName = "ziose-${project.hasProperty('type') ? 'test' : 'prod'}-${project.ext.version}"
  destinationDirectory = project.ext.outputDir
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE

  from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  if (project.hasProperty('type')) {
    from sourceSets.toSet().output
  }

  manifest {
    attributes 'Main-Class': 'com.cloudant.ziose.experiments.Hello'
  }
}
